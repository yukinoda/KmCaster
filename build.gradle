plugins {
  id 'application'
}

group 'com.whitemagicsoftware'
version '1.0'

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  // Command-line parsing
  implementation 'info.picocli:picocli:4.4.0'

  // SVG
  implementation fileTree(include: ['**/*.jar'], dir: 'libs')

  implementation 'com.1stleg:jnativehook:2.1.0'
}

sourceSets {
  main {
    java {
      srcDirs = ["src/main"]
    }
  }
}

compileJava {
  options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

application {
  applicationName = 'kmcaster'
  mainClassName = "com.whitemagicsoftware.${applicationName}.KmCaster"
}

jar {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE

  manifest {
    attributes 'Main-Class': mainClassName
  }

  from {
    (configurations.runtimeClasspath.findAll { !it.path.endsWith(".pom") }).collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }

  archiveFileName = "${applicationName}.jar"

  exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}
